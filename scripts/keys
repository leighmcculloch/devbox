#! /usr/bin/env zsh

function title {
  print -P "%F{green}$1%f"
}

# Collect user info
title 'Collecting user info...'
host="$HOST-$(hostname)"
timestamp="$(date +%Y%m%d-%I%M%p-%Z-%a | tr '[:lower:]' '[:upper:]')"
name=$(git config --global user.name)
email=$(git config --global user.email)
vared -p 'Name: ' -c name
vared -p 'Email: ' -c email
identity=$(echo "$name ($host) ($timestamp) <$email>")
echo "Identity: $identity"
echo

function getgpgkey() {
  local keynum=$1
  local keydetails=$(gpg --show-keys --with-keygrip --with-colons <(gpg --armor --export "$identity"))
  keyfingerprint=$(echo "$keydetails" | grep '^fpr' | head -n $keynum | tail -n 1 | cut -d':' -f10)
  keygrip=$(echo "$keydetails" | grep '^grp' | head -n $keynum | tail -n 1 | cut -d':' -f10)
  keyid=$(echo "$keydetails" | grep '^[ps]ub' | head -n $keynum | tail -n 1 | cut -d':' -f5)
}

# Generate key for user
title 'Generating gpg key...'
gpg --batch --quick-generate-key "$identity" future-default sign 5d
getgpgkey 1
keyid1=$keyid
keyfingerprint1=$keyfingerprint
title "Master: $keyid1 / $keyfingerprint ($keygrip)"
gpg --yes --batch -v --quick-addkey "$keyfingerprint1" ed25519 sign 5d
getgpgkey 2
keyid2=$keyid
title "Sign (Git Commit): $keyid2 / $keyfingerprint ($keygrip)"
gpg --yes --batch -v --quick-addkey "$keyfingerprint1" ed25519 sign,auth 5d
getgpgkey 3
keyid3=$keyid
title "Sign,Auth (SSH): $keyid3 / $keyfingerprint ($keygrip)"
echo "$keygrip 7200" >> "$HOME/.gnupg/sshcontrol"
echo

# Upload keys
title 'Upload keys to %F{cyan}https://github.com/settings/keys'
title 'SSH:'
echo $(gpg --export-ssh-key "$keyid3" | cut -d' ' -f'1,2') $(echo $identity | tr ' ' '-')
title 'GPG:'
gpg --armor --export "$keyid2"
echo

# Wait
title 'Press any key to continue...'
read -k1 -s
echo

# Import known GPG key for GitHub
title 'Importing known gpg keys...'
curl -sSL https://github.com/leighmcculloch.gpg | gpg --import
curl -sSL https://github.com/web-flow.gpg | gpg --import

# Configure user
title 'Configuring git...'
git config -f "$HOME/.gitconfig.local" user.name "$name"
git config -f "$HOME/.gitconfig.local" user.email "$email"
git config -f "$HOME/.gitconfig.local" user.signingkey "$keyid2!"
git config -f "$HOME/.gitconfig.local" commit.gpgsign true
