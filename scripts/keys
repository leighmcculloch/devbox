#! /usr/bin/env zsh

function title {
  print -P "%F{green}$1%f"
}

# Collect user info
title 'Collecting user info...'
host="$HOST-$(hostname)"
timestamp="$(date +%Y%m%d%a | tr '[:lower:]' '[:upper:]')"
name=$(git config --global user.name)
email=$(git config --global user.email)
vared -p 'Name: ' -c name
vared -p 'Email: ' -c email
identity="$name ($host) [$timestamp] <$email>"
echo "Identity: $identity"
echo

# Generate key for user
title 'Generating gpg key...'
gpg --batch --quick-generate-key "$identity" future-default sign 5d
keydetails1=$(gpg --show-keys --with-keygrip --with-colons <(gpg --armor --export "$identity"))
fingerprint1=$(echo "$keydetails1" | grep '^fpr' | head -n 1 | sed -e 's/[fpr:]//g')
keygrip1=$(echo "$keydetails1" | grep '^grp' | head -n 1 | sed -e 's/[grp:]//g')
echo "Master: $fingerprint1 ($keygrip1)"
gpg --yes --batch -v --quick-addkey "$fingerprint1" ed25519 sign,auth 5d
keydetails2=$(gpg --show-keys --with-keygrip --with-colons <(gpg --armor --export "$identity"))
fingerprint2=$(echo "$keydetails2" | grep '^fpr' | head -n 2 | tail -n 1 | sed -e 's/[fpr:]//g')
keygrip2=$(echo "$keydetails2" | grep '^grp' | head -n 2 | tail -n 1| sed -e 's/[grp:]//g')
echo "Sign,Auth: $fingerprint2 ($keygrip2)"
echo "$keygrip2 7200" >> "$HOME/.gnupg/sshcontrol"
echo

# Upload keys
title 'Upload keys to %F{cyan}https://github.com/settings/keys'
title 'SSH:'
gpg --export-ssh-key "$identity"
title 'GPG:'
gpg --armor --export "$identity"
echo

# Wait
title 'Press any key to continue...'
read -k1 -s
echo

# Import known GPG key for GitHub
title 'Importing known gpg keys...'
curl -sSL https://github.com/leighmcculloch.gpg | gpg --import
curl -sSL https://github.com/web-flow.gpg | gpg --import

# Configure user
title 'Configuring git...'
git config -f "$HOME/.gitconfig.local" user.name "$name"
git config -f "$HOME/.gitconfig.local" user.email "$email"
git config -f "$HOME/.gitconfig.local" user.signingkey "$name"
git config -f "$HOME/.gitconfig.local" commit.gpgsign true
